plugins {
    alias(libs.plugins.spring.boot)
}

apply plugin: 'io.spring.dependency-management'

dependencies {

    // 其他子项目
    api project(':playground-include')

    // spring-boot and spring
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-logging'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-aop'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-redis'
    compileOnly 'org.springframework:spring-context-indexer'
    annotationProcessor 'org.springframework:spring-context-indexer'

    // spring-security
    testImplementation 'org.springframework.security:spring-security-test'

    // spring-retry
    implementation 'org.springframework.retry:spring-retry'

    // spring-turbo
    implementation(libs.spring.turbo)
    implementation(libs.spring.turbo.jackson)
    implementation(libs.spring.turbo.webmvc)
    implementation(libs.spring.turbo.webcli)
    implementation(libs.spring.turbo.security)
    implementation(libs.spring.turbo.jwt)
    implementation(libs.spring.turbo.misc)
    implementation(libs.spring.turbo.redis)
    implementation(libs.spring.turbo.jdbc)

    // DB相关
    implementation(libs.mysql)
    implementation(libs.bundles.mybatis)
    implementation(libs.p6spy.spring.boot)

    // redisson / redlock
    implementation(libs.redisson.redlock.spring.boot)

    // lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'

    // mapstruct
    implementation(libs.mapstruct)
    annotationProcessor(libs.mapstruct)

    // swagger
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.7.0'

    // 通用工具
    implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.apache.commons:commons-text:1.13.0'
    implementation 'commons-codec:commons-codec:1.17.1'
    implementation 'org.apache.commons:commons-collections4:4.4'
    implementation 'commons-io:commons-io:2.18.0'
    implementation 'org.apache.commons:commons-pool2'
    implementation('com.google.guava:guava:33.4.0-jre') {
        transitive = false
    }

}

jar {
    enabled = false
}

springBoot {
    buildInfo {
        enabled = true
        excludes = ['group', 'artifact']
        properties {
            additional = ['name'        : 'playground',
                          'version'     : "${rootProject.version}",
                          'time'        : "${rootProject.buildTimestamp}",
                          'tool-type'   : 'gradle',
                          'tool-version': gradle.gradleVersion
            ]
        }
    }

    mainClass = 'com.github.yingzhuo.playground.ApplicationBoot'
}

bootJar {
    manifest {
        attributes(['Start-Class'           : 'com.github.yingzhuo.playground.ApplicationBoot',
                    'Main-Class'            : 'org.springframework.boot.loader.launch.PropertiesLauncher',
                    'Implementation-Version': "${rootProject.version}",
                    'Implementation-Title'  : 'playground',
        ])
    }

    archiveFileName = "playground-${rootProject.version}.${archiveExtension.get()}"

    includeTools = false

    layered {
        enabled = true

        application {
            intoLayer("spring-boot-loader") {
                include "org/springframework/boot/loader/**"
            }
            intoLayer("application")
        }

        dependencies {
            intoLayer("application") {
                includeProjectDependencies()
            }

            intoLayer("snapshot-dependencies") {
                include "*:*:*SNAPSHOT"
            }

            intoLayer("dependencies")
        }

        layerOrder = ["dependencies", "spring-boot-loader", "snapshot-dependencies", "application"]
    }

    def excludePatterns = [
            '**/.DS_Store',
            '**/.gitkeep',
            '**/junit-*.jar',
            '**/mockito-*.jar',
            '**/opentest4j-*.jar',
            '**/spring-test-*.jar',
            '**/spring-boot-test-*.jar',
            '**/spring-boot-configuration-processor-*.jar',
            '**/spring-security-test-*.jar',
            '**/log4j-api-*.jar',
            '**/log4j-to-slf4j-*.jar'
    ]

    if (project.hasProperty('build-for-prod')) {
        excludePatterns += [
                '**/logback-spring.xml',
                '**/application-dev.y?ml'
        ]
    }

    excludes = excludePatterns

}

test {
    useJUnitPlatform()
}
